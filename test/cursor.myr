use std
use testr

use xml

const main = {
	testr.run([
		[.name="simple", .fn={ctx
			simple_test(ctx)
		}],
		[.name="eof", .fn={ctx
			eof_test(ctx)
		}],
		[.name="seek", .fn={ctx
			seek_test(ctx)
		}]
	][:])
}

const compare = {ctx, x, tests
	var g, e
	var res, expect

	for (fun, ex) : tests
		res = fun(x)
		expect = ex

		g = std.fmt("{}", res)
		e = std.fmt("{}", expect)

		if !std.sleq(g, e)
			testr.fail(ctx, "got \"{}\" expected \"{}\"", g, e)
		;;

		std.slfree(g)
		std.slfree(e)

		match res
		| `std.Ok ev:	xml.eventfree(ev)
		| `std.Err _:
		;;
	;;
}

const comparebuf = {ctx, buf, tests
	var x = std.try(xml.mkbuf(buf))
	compare(ctx, x, tests)
	xml.free(x)
}

const simple_test = {ctx
	var t : ((x : xml.ctx# -> std.result(xml.event, xml.err)), \
			std.result(xml.event, xml.err))[:]

	t = [
		({x; -> xml.next(x);}, `std.Ok `xml.Start ("doc", [][:])),
		({x; -> xml.next(x);}, `std.Ok `xml.End "doc"),
		({x; -> xml.next(x);}, `std.Ok `xml.Eof)
	][:]

	comparebuf(ctx, "<doc></doc>", t)
}

const eof_test = {ctx
	comparebuf(ctx, "<doc></doc>", [
		({x; -> xml.next(x);}, `std.Ok `xml.Start ("doc", [][:])),
		({x; -> xml.next(x);}, `std.Ok `xml.End "doc"),
		({x; -> xml.next(x);}, `std.Ok `xml.Eof),
		({x; -> xml.next(x);}, `std.Ok `xml.Eof),
		({x; -> xml.next(x);}, `std.Ok `xml.Eof)
	][:])
}

const seek_test = {ctx
	comparebuf(ctx, "<one><two><three>four</three></two></one>", [
		({x; -> xml.seek(x, "three");},
			`std.Ok `xml.Start ("three", [][:])),
		({x; -> xml.next(x);}, `std.Ok `xml.Characters "four"),
		({x; -> xml.seekend(x, "two");}, `std.Ok `xml.End "two"),
		({x; -> xml.next(x);}, `std.Ok `xml.End "one"),
		({x; -> xml.next(x);}, `std.Ok `xml.Eof)
	][:])

	comparebuf(ctx, "<one></one>", [
		({x; -> xml.seek(x, "absent");}, `std.Ok `xml.Eof),
		({x; -> xml.next(x);}, `std.Ok `xml.Eof)
	][:])

	comparebuf(ctx, "<one><two><three>four</three></two></one>", [
		({x; -> xml.next(x);}, `std.Ok `xml.Start ("one", [][:])),
		({x; -> xml.seek(x, "two");},
			`std.Ok `xml.Start ("two", [][:])),
		({x; -> xml.seek(x, "three");},
			`std.Ok `xml.Start ("three", [][:])),
		({x; -> xml.seek(x, "four");}, `std.Ok `xml.Eof),
		({x; -> xml.next(x);}, `std.Ok `xml.Eof)
	][:])
}
