use std
use testr

use xml

const main = {
	testr.run([
		[.name="simple", .fn={ctx
			simple_test(ctx)
		}],
		[.name="eof", .fn={ctx
			eof_test(ctx)
		}],
		[.name="seek", .fn={ctx
			seek_test(ctx)
		}]
	][:])
}

const simple_test = {ctx
	var x = std.try(xml.mkbuf("<doc></doc>"))

	match xml.next(x)
	| `std.Ok `xml.Start ("doc", _):
		testr.ok(ctx)
	| `std.Ok e:
		testr.fail(ctx, "<doc>: event {}", e)
	| `std.Err e:
		testr.fail(ctx, "<doc>: error {}", e)
	;;

	match xml.next(x)
	| `std.Ok `xml.End "doc":
		testr.ok(ctx)
	| `std.Ok e:
		testr.fail(ctx, "</doc>: event {}", e)
	| `std.Err e:
		testr.fail(ctx, "</doc>: error {}", e)
	;;

	match xml.next(x)
	| `std.Ok `xml.Eof:
		testr.ok(ctx)
	| `std.Ok e:
		testr.fail(ctx, "eof: event {}", e)
	| `std.Err e:
		testr.fail(ctx, "eof: error {}", e)
	;;

	xml.free(x)
}

const eof_test = {ctx
	var x = std.try(xml.mkbuf("<doc></doc"))

	xml.next(x)
	xml.next(x)

	match xml.next(x)
	| `std.Ok `xml.Eof:	testr.ok(ctx)
	| a:			testr.fail(ctx, "eof: {}", a)
	;;

	match xml.next(x)
	| `std.Ok `xml.Eof:	testr.ok(ctx)
	| a:			testr.fail(ctx, "eof again: {}", a)
	;;

	xml.free(x)
}

const seek_test = {ctx
	var x = std.try(xml.mkbuf("<one><two><three>four</three></two></one>"))

	match xml.seek(x, "three")
	| `std.Ok `xml.Start ("three", _):	testr.ok(ctx)
	| e:	testr.fail(ctx, "<three>: got {}", e)
	;;

	match xml.next(x)
	| `std.Ok `xml.Characters "four":	testr.ok(ctx)
	| e:	testr.fail(ctx, "four: got {}", e)
	;;

	match xml.seekend(x, "two")
	| `std.Ok `xml.End "two":	testr.ok(ctx)
	| e:	testr.fail(ctx, "</two>: {}", e)
	;;

	match xml.next(x)
	| `std.Ok `xml.End "one":	testr.ok(ctx)
	| e:	testr.fail(ctx, "</one>: got {}", e)
	;;

	match xml.next(x)
	| `std.Ok `xml.Eof:	testr.ok(ctx)
	| e:	testr.fail(ctx, "eof: got {}", e)
	;;

	xml.free(x)
}
