use std
use xml
use testr

const main = {
	testr.run([
		[.name="simple", .fn={ctx
			simple_test(ctx)
		}],
		[.name="attrs", .fn={ctx
			attrs_test(ctx)
		}],
		[.name="empty", .fn={ctx
			empty_test(ctx)
		}],
		[.name="twigil", .fn={ctx
			twigil_test(ctx)
		}],
		[.name="crlf", .fn={ctx
			crlf_test(ctx)
		}],
		[.name="doctype", .fn={ctx
			doctype_test(ctx)
		}],
		[.name="space", .fn={ctx
			space_test(ctx)
		}],
		[.name="bad", .fn={ctx
			bad_test(ctx)
		}]
	][:])
}

const checkevents = {ctx, x, expect : std.result(xml.event, xml.err)[:]
	var i = 0
	var g, e

	for ev : xml.byevent(x)
		if i >= expect.len
			testr.fail(ctx,
				"too many events, {}>{}", i+1, expect.len)
			-> void
		;;
		g = std.fmt("{}", ev)
		e = std.fmt("{}", expect[i])
		if !std.sleq(g, e)
			testr.fail(ctx, "got \"{}\" expected \"{}\"", g, e)
		;;
		std.slfree(g)
		std.slfree(e)
		i++
	;;

	testr.check(ctx, i == expect.len,
		"too few events {}<{}", i, expect.len)
}

const compare = {ctx, path, expect
	var x = std.try(xml.mkpath(path))
	checkevents(ctx, x, expect)
	std.free(x)
}

const comparebuf = {ctx, str, expect
	var x = std.try(xml.mkbuf(str))
	checkevents(ctx, x, expect)
	xml.free(x)
}

const simple_test = {ctx
	compare(ctx, "test/files/001_simple.xml", [
		`std.Ok `xml.Start ("doc", [][:]),
		`std.Ok `xml.Start ("children", [][:]),
		`std.Ok `xml.Start ("child", [][:]),
		`std.Ok `xml.Start ("id", [][:]),
		`std.Ok `xml.Characters "1",
		`std.Ok `xml.End "id",
		`std.Ok `xml.End "child",
		`std.Ok `xml.Start ("child", [][:]),
		`std.Ok `xml.Start ("id", [][:]),
		`std.Ok `xml.Characters "2",
		`std.Ok `xml.End "id",
		`std.Ok `xml.End "child",
		`std.Ok `xml.Start ("child", [][:]),
		`std.Ok `xml.Start ("id", [][:]),
		`std.Ok `xml.Characters "3",
		`std.Ok `xml.End "id",
		`std.Ok `xml.End "child",
		`std.Ok `xml.End "children",
		`std.Ok `xml.End "doc",
		`std.Ok `xml.Eof
	][:])
}

const attrs_test = {ctx
	compare(ctx, "test/files/002_attrs.xml", [
		`std.Ok `xml.Start ("doc", [
			("one", "1"),
			("two", "two"),
			("three", "..."),
			("four", "fo>r")
		][:]),
		`std.Ok `xml.End "doc",
		`std.Ok `xml.Eof
	][:])
}

const empty_test = {ctx
	compare(ctx, "test/files/003_empty.xml", [
		`std.Ok `xml.Start ("doc", [][:]),
		`std.Ok `xml.Start ("empty", [][:]),
		`std.Ok `xml.End "empty",
		`std.Ok `xml.Start ("empty", [][:]),
		`std.Ok `xml.End "empty",
		`std.Ok `xml.End "doc",
		`std.Ok `xml.Eof
	][:])
}

const twigil_test = {ctx
	compare(ctx, "test/files/004_twigil.xml", [
		`std.Ok `xml.Instruction ("xml", "version=\"1.0\""),
		`std.Ok `xml.Doctype "test [\n\t<!ENTITY x \"example\">\n]",
		`std.Ok `xml.Comment "This is a comment",
		`std.Ok `xml.Start ("doc", [][:]),
		`std.Ok `xml.Cdata "This is cdata",
		`std.Ok `xml.End "doc",
		`std.Ok `xml.Eof
	][:])
}

const crlf_test = {ctx
	compare(ctx, "test/files/005_crlf.xml", [
		`std.Ok `xml.Start ("doc", [][:]),
		`std.Ok `xml.Comment "document contains crlf line endings",
		`std.Ok `xml.End "doc",
		`std.Ok `xml.Eof
	][:])
}

const doctype_test = {ctx
	var x
	var o

	x = std.try(xml.mkpath("test/files/006a_doctype.xml"))
	o = 0

	for ev : xml.byevent(x)
		match ev
		| `std.Ok `xml.Doctype "doc [\n\t<!ENTITY t \"<t&gt;\">\n]":
			o++
		| `std.Ok `xml.Start ("doc", _):
			o++
		| `std.Ok `xml.End "doc":
			o++
		| `std.Ok `xml.Eof:
			o++
		| e:
			testr.fail(ctx, "unexpected {}", e)
		;;
	;;

	testr.check(ctx, o == 4, "unexpected event count {}", o)

	xml.free(x)

	x = std.try(xml.mkpath("test/files/006b_doctype.xml"))
	o = 0

	for ev : xml.byevent(x)
		match ev
		| `std.Ok `xml.Doctype "doc [\n\t<!ENTITY t '<t&gt;'>\n]":
			o++
		| `std.Ok `xml.Start ("doc", _):
			o++
		| `std.Ok `xml.End "doc":
			o++
		| `std.Ok `xml.Eof:
			o++
		| e:
			testr.fail(ctx, "unexpected {}", e)
		;;
	;;

	testr.check(ctx, o == 4, "unexpected event count {}", o)

	xml.free(x)

/* XXX
	x = std.try(xml.mkpath("test/files/006c_doctype.xml"))
	o = 0

	for ev : xml.byevent(x)
		match ev
		| `std.Ok `xml.Doctype "doc [\n\t<!-- shouldn't fail -->\n]":
			o++
		| `std.Ok `xml.Start ("doc", _):
			o++
		| `std.Ok `xml.End "doc":
			o++
		| `std.Ok `xml.Eof:
			o++
		| e:
			testr.fail(ctx, "unexpected {}", e)
		;;
	;;

	testr.check(ctx, o == 4, "unexpected event count {}", o)

	xml.free(x)
*/
}

const space_test = {ctx
	compare(ctx, "test/files/009_space.xml", [
		`std.Ok `xml.Start ("doc", [][:]),
		`std.Ok `xml.End "doc",
		`std.Ok `xml.Eof
	][:])
}

const bad_test = {ctx
	comparebuf(ctx, "<doc><.child></.child></doc>", [
		`std.Ok `xml.Start ("doc", [][:]),
		`std.Err [.line=1, .off=7, .err=`xml.Inval '.']
	][:])

	comparebuf(ctx, "<??><doc></doc>", [
		`std.Err [.line=1, .off=4, .err=`xml.Empty]
	][:])

	comparebuf(ctx, "<doc></doc>doc", [
		`std.Ok `xml.Start ("doc", [][:]),
		`std.Ok `xml.End "doc",
		`std.Err [.line=1, .off=14, .err=`xml.Trunc `bio.Eof]
	][:])

	comparebuf(ctx, "<doc>\n<?target example instruction>\n</doc>", [
		`std.Ok `xml.Start ("doc", [][:]),
		`std.Err [.line=2, .off=1, .err=`xml.Unclosed ("<?", "?>")]
	][:])

	comparebuf(ctx, "<doc an></doc>", [
		`std.Err [.line=1, .off=8, .err=`xml.Inval '>']
	][:])

	comparebuf(ctx, "<doc an=val></doc>", [
		`std.Err [.line=1, .off=9, .err=`xml.Unexpected ('v', '"')]
	][:])

	comparebuf(ctx, "<doc an=></doc>", [
		`std.Err [.line=1, .off=9, .err=`xml.Unexpected ('>', '"')]
	][:])

	comparebuf(ctx, "<doc an=\"val\" \"val\"></doc>", [
		`std.Err [.line=1, .off=15, .err=`xml.Inval '"']
	][:])

	comparebuf(ctx, "<doc></>", [
		`std.Ok `xml.Start ("doc", [][:]),
		`std.Err [.line=1, .off=8, .err=`xml.Inval '>']
	][:])

	comparebuf(ctx, "<doc><![CDATA[</doc>", [
		`std.Ok `xml.Start ("doc", [][:]),
		`std.Err [
			.line=1,
			.off=7,
			.err=`xml.Unclosed ("<![CDATA[", "]]>")
		]
	][:])
}
