use std

pkg xml =
	const decode	: (s : byte[:] -> std.result(byte[:], byte[:]))
	const decodef	: (s : byte[:] -> byte[:])
;;

const decode = {s
	var sb = std.mksb()
	var start, idx

	start = idx = 0

	while idx < s.len
		match std.decode(s[idx:])
		| '&':	match unencode(s[idx:])
			| `std.Some (b, i):
				std.sbputs(sb, s[start:idx])
				std.sbputb(sb, b)

				idx += i
				start = idx
			| `std.None:
				goto err
			;;
		| c:	idx += std.charlen(c)
		;;
	;;

	std.sbputs(sb, s[start:])

	-> `std.Ok std.sbfin(sb)

:err
	std.sbfree(sb)
	-> `std.Err s[idx : idx + errend(s[idx:])]
}

const decodef = {s
	var sb = std.mksb()
	var start, idx

	start = idx = 0

	while idx < s.len
		match std.decode(s[idx:])
		| '&':	match unencode(s[idx:])
			| `std.Some (b, i):
				std.sbputs(sb, s[start:idx])
				std.sbputb(sb, b)

				idx += i
				start = idx
			| `std.None:
				idx += std.charlen('&')
			;;
		| c:	idx += std.charlen(c)
		;;
	;;

	std.sbputs(sb, s[start:])

	-> std.sbfin(sb)
}

const unencode = {s
	const cr = "&#"
	const ens = [
		("&amp;", '&'),
		("&apos;", '\''),
		("&lt;", '<'),
		("&gt;", '>'),
		("&quot;", '"')
	][:]

	if std.hasprefix(s, cr)
		-> charref(s, cr.len)
	;;

	for (name, char) : ens
		if s.len >= name.len && std.sleq(name, s[:name.len])
			-> `std.Some ((char : byte), name.len)
		;;
	;;

	-> `std.None
}

const charref = {s, idx
	var end
	var base

	end = idx

	match findsc(s)
	| `std.Some i:	end = i
	| `std.None:	-> `std.None
	;;

	match std.decode(s[idx:])
	| 'x':
		base = 16
		idx += std.charlen('x')
	| _:
		base = 10
	;;

	if end - idx < 1
		-> `std.None
	;;

	match std.intparsebase(s[idx:end], base)
	| `std.Some i:	-> `std.Some (i, end + std.charlen(';'))
	| `std.None:	-> `std.None
	;;
}

const findsc = {s
	var c
	for var i = 0; i < s.len; i += std.charlen(c)
		c = std.decode(s[i:])
		if c == ';'
			-> `std.Some i
		;;
	;;

	-> `std.None
}

const errend = {s
	var i = 0
	var c

	while i < s.len
		c = std.decode(s[i:])
		if c == ';'
			i += std.charlen(c)
			break
		elif c == '\r' || std.isspace(c)
			break
		else
			i += std.charlen(c)
		;;
	;;

	-> i
}
