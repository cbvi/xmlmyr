use std
use bio

use "buf"
use "chars"
use "err"
use "types"

pkg xml =
	pkglocal const parsedoctype : (x : ctx# -> std.result(event, errtype))
;;

const parsedoctype = {x
	const pref = "DOCTYPE"
	var a, z
	var open

	match bio.readterm(x.file, ">")
	| `std.Ok b:	newbuf(x, b)
	| `std.Err e:	-> `std.Err `Trunc e
	;;

	match bexpect(x, pref)
	| `std.Ok _:	x.p.off += 7
	| `std.Err e:	-> `std.Err e
	;;

	match bget(x)
	| `std.Some c:
		if !isspace(c)
			-> `std.Err `Unexpected (c, ' ')
		;;
	| `std.None:	-> `std.Err `Unclosed ("<!DOCTYPE", ">")
	;;

	beatspace(x)

	a = x.p.bidx
	open = 1

	while x.p.bidx < x.p.buf.len
		match bnext(x)
		| '<':
			if std.hasprefix(x.p.buf[x.p.bidx:], "!--")
				docomment(x)
			else
				open++
			;;
		| '"':	doquote(x, '"')
		| '\'':	doquote(x, '\'')
		| '>':
			open--
			if open == 0
				z = x.p.bidx - std.charlen('>')
				-> `std.Ok `Doctype prune(x.p.buf[a:z])
			else
				refill(x)
			;;
		| _:
		;;
	;;

	-> `std.Err `Trunc `bio.Eof
}

const refill = {x
	match bio.readterm(x.file, ">")
	| `std.Ok b:	expandbuf(x, b)
	| `std.Err e:
	;;
}

const doquote = {x, q
	var c
	while x.p.bidx < x.p.buf.len
		c = bnext(x)
		if c == q
			break
		elif c == '>'
			refill(x)
		;;
	;;
}

const docomment = {x
	var end = 0

	bskip(x, "!--".len)

	while x.p.bidx < x.p.buf.len
		match bnext(x)
		| '>':
			refill(x)
			if end > 1
				break
			else
				end = 0
			;;
		| '-':	end++
		| _:
			end = 0
		;;
	;;
}
