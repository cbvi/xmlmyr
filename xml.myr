use std
use bio

use "attributes"
use "buf"
use "chars"
use "doctype"
use "err"
use "mk"
use "types"

pkg xml =
	pkglocal const dispatch	: (x : ctx# -> std.result(event, err))
;;

const dispatch = {x
	match x.p.state
	| `Open:	-> open(x)
	| `Close:	-> close(x)
	| `Closing n:	-> closing(x, n)
	;;
}

const close = {x
	var start, stop

	match bio.readterm(x.file, "<")
	| `std.Ok s:
		newbuf(x, s)
	| `std.Err `bio.Eof:
		x.p.more = false
		-> `std.Ok `Eof
	| `std.Err e:
		-> `std.Err error(x, `Trunc e)
	;;

	start = stop = x.p.bidx

	while x.p.bidx < x.p.buf.len
		if bnext(x) == '<'
			x.p.state = `Open
			if prune(x.p.buf[start:stop]).len > 0
				-> `std.Ok `Characters x.p.buf[start:stop]
			else
				-> dispatch(x)
			;;
		;;
		stop = x.p.bidx
	;;

	if prune(x.p.buf).len > 0
		-> `std.Ok `Characters x.p.buf[start:stop]
	else
		x.p.more = false
		-> `std.Ok `Eof
	;;
}

const open = {x
	x.p.state = `Close

	match bio.peekc(x.file)
	| `std.Ok '!':	-> parseexclamation(x)
	| `std.Ok '/':	-> parseend(x)
	| `std.Ok '?':	-> parseinstruction(x)
	| `std.Ok _:	-> parseelemname(x)
	| `std.Err e:	-> `std.Err error(x, `Trunc e)
	;;
}

const closing = {x, name
	x.p.state = `Close
	-> `std.Ok `End name
}

const parseelemname = {x
	var start, stop
	var cnt = 0

	match bio.readterm(x.file, ">")
	| `std.Ok s:	newbuf(x, s)
	| `std.Err e:	-> `std.Err error(x, `Trunc e)
	;;

	match startname(x)
	| `std.Ok o:	(start, stop) = o
	| `std.Err e:	-> `std.Err e
	;;

	while x.p.bidx < x.p.buf.len
		match bnext(x)
		| '>':	goto end
		| '/':	goto selfclose
		| c:
			if namechar(c)
				stop = x.p.bidx
			elif isspace(c)
				goto attrs
			else
				-> `std.Err error(x, `Inval c)
			;;
		;;
	;;

	goto unclosed

:attrs
	match parseattrs(x)
	| `std.Ok i:	cnt = i
	| `std.Err e:	-> `std.Err e
	;;

:ending
	if x.p.bidx < x.p.buf.len
		match bnext(x)
		| '>':	/* fallthrough to end */
		| '/':	goto selfclose
		| c:
			-> `std.Err error(x, `Unexpected (c, '>'))
		;;
	else
		goto unclosed
	;;

:end
	-> `std.Ok `Start (x.p.buf[start:stop], x.a.attrs[:cnt])

:selfclose
	match bexpect(x, '>')
	| `std.Ok _:
		x.p.state = `Closing x.p.buf[start:stop]
		-> `std.Ok `Start (x.p.buf[start:stop], x.a.attrs[:cnt])
	| `std.Err `Trunc `bio.Eof:
		-> `std.Err error(x, `Unclosed ("<", "/>"))
	| `std.Err e:
		-> `std.Err error(x, e)
	;;

:unclosed
	-> `std.Err error(x, `Unclosed ("<", ">"))
}

const parseexclamation = {x
	match bio.getc(x.file)
	| `std.Ok c:
		std.assert(c == '!', "BUG: parseexclamation() ! missing\n")
		x.p.off++
	| `std.Err e:	-> `std.Err error(x, `Trunc e)
	;;

	match bio.peekc(x.file)
	| `std.Ok '[':	-> parsecdata(x)
	| `std.Ok '-':	-> parsecomment(x)
	| `std.Ok 'D':	-> parsedoctype(x)
	| `std.Ok c:	-> `std.Err error(x, `Inval c)
	| `std.Err e:	-> `std.Err error(x, `Trunc e)
	;;
}

const parseend = {x
	var start, stop

	match bio.readterm(x.file, ">")
	| `std.Ok s:	newbuf(x, s)
	| `std.Err e:	-> `std.Err error(x, `Trunc e)
	;;

	/* to get here peek has to show a / was next up */
	std.assert(bnext(x) == '/', "BUG: parseend() / missing\n")

	match getname(x)
	| `std.Ok o:			(start, stop) = o
	| `std.Err `Trunc `bio.Eof:	goto unclosed
	| `std.Err e:			-> `std.Err error(x, e)
	;;

	beatspace(x)

	match bexpect(x, '>')
	| `std.Ok _:			-> `std.Ok `End x.p.buf[start:stop]
	| `std.Err `Trunc `bio.Eof:	goto unclosed
	| `std.Err e:			-> `std.Err error(x, e)
	;;

:unclosed
	-> `std.Err error(x, `Unclosed ("</", ">"))
}

const parsecdata = {x
	const pre = "CDATA[" /* ![ is already checked */
	const suf = "]]>"
	var nidx
	var st
	var end

	match bio.readterm(x.file, ">")
	| `std.Ok b:	newbuf(x, b)
	| `std.Err e:	-> `std.Err error(x, `Trunc e)
	;;

	/* to get here peek has to show a [ was next up */
	std.assert(bnext(x) == '[', "BUG: parsecdata() '[' missing\n")

	nidx = x.p.bidx + pre.len

	if nidx < x.p.buf.len && std.sleq(x.p.buf[x.p.bidx:nidx], pre)
		x.p.bidx = nidx
		x.p.off += 6
	else
		/* match failed so determine what the error is */
		for c : std.bychar(pre)
			match bexpect(x, c)
			| `std.Err e:	-> `std.Err error(x, e)
			| _:
			;;
		;;

		/* a document that ends with "<![CDATA[" will reach
		 * here, let it keep going and error naturally
		 */
	;;

	st = x.p.bidx
	end = 0

:again
	while x.p.bidx < x.p.buf.len
		match bnext(x)
		| ']':	end++
		| '>':	if end > 1
				-> `std.Ok `Cdata x.p.buf[st:x.p.bidx-suf.len]
			;;
		| _:	end = 0
		;;
	;;

	match bio.readterm(x.file, ">")
	| `std.Ok b:	expandbuf(x, b)
	| `std.Err `bio.Eof:
			-> `std.Err error(x, `Unclosed ("<![CDATA[", "]]>"))
	| `std.Err e:	-> `std.Err error(x, `Trunc e)
	;;

	goto again
}

const parsecomment = {x
	const suf = "-->"
	var c, end
	var st

	match bio.readterm(x.file, ">")
	| `std.Ok b:	newbuf(x, b)
	| `std.Err e:	-> `std.Err error(x, `Trunc e)
	;;

	/* to get here peek has to show a - was next up */
	std.assert(bnext(x) == '-', "BUG: parsecomment() '-' missing\n")

	/* another - is needed but this one isn't guaranteed */
	match bexpect(x, '-')
	| `std.Ok _:
	| `std.Err e:	-> `std.Err error(x, e)
	;;

	end = 0
	st = x.p.bidx

:again
	while x.p.bidx < x.p.buf.len
		c = bnext(x)

		if end == 2
			if c != '>'
				/* stray -- in comments is invalid */
				-> `std.Err error(x, `Unexpected (c, '>'))
			;;
			-> `std.Ok `Comment prune(x.p.buf[st:x.p.bidx-suf.len])
		elif c == '-'
			end++
		else
			end = 0
		;;
	;;

	match bio.readterm(x.file, ">")
	| `std.Ok b:	expandbuf(x, b)
	| `std.Err `bio.Eof:
			-> `std.Err error(x, `Unclosed ("<!--", "-->"))
	| `std.Err e:	-> `std.Err error(x, `Trunc e)
	;;

	goto again
}

const parseinstruction = {x
	const suf = "?>"
	var ts, te
	var ds
	var end
	var err

	match bio.readterm(x.file, ">")
	| `std.Ok b:	newbuf(x, b)
	| `std.Err e:	err = `Trunc e ; goto err
	;;

	/* to get here peek has to show a ? was next up */
	std.assert(bnext(x) == '?', "BUG: parseinstruction() '?' missing\n")

	match getname(x)
	| `std.Ok o:	(ts, te) = o
	| `std.Err e:	-> `std.Err error(x, e)
	;;

	match bget(x)
	| `std.Some '?':
		match bexpect(x, '>')
		| `std.Ok _:	-> `std.Ok `Instruction (x.p.buf[ts:te], "")
		| `std.Err `Trunc `bio.Eof:	goto unclosed
		| `std.Err e:	err = e ; goto err
		;;
	| `std.Some c:
		if isspace(c)
			beatspace(x)
		else
			err = `Inval c ; goto err
		;;
	| `std.None:
		goto unclosed
	;;

	end = false
	ds = x.p.bidx

:again
	while x.p.bidx < x.p.buf.len
		match bnext(x)
		| '?':	end = true
		| '>':	if end
				-> `std.Ok `Instruction (x.p.buf[ts:te],
					x.p.buf[ds:x.p.bidx-suf.len])
			;;
		| _:	end = false
		;;
	;;

	match bio.readterm(x.file, ">")
	| `std.Ok b:		expandbuf(x, b)
	| `std.Err `bio.Eof:	goto unclosed
	| `std.Err e:		err = `Trunc e ; goto err
	;;

	goto again

:unclosed
	err = `Unclosed ("<?", "?>")

:err
	-> `std.Err error(x, err)
}
