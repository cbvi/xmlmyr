use std
use bio

use "chars"

pkg xml =
	const mkbuf : (s : byte[:] -> xml#)
	const parse : (x : xml# -> std.result(event, error))
	const free  : (x : xml# -> void)

	type xmleventiter = xml
	//impl iterable xmleventiter -> std.result(event, error)
;;

type xml = struct
	file	:	bio.file#
	buf	:	byte[:]
	bidx	:	std.size
	bcur	:	std.size
	line	:	std.size
	off	:	std.size
	state	:	state
;;

type error = struct
	line	:	std.size
	off	:	std.size
	err	:	errtype
;;

type errtype = union
	`Empty
	`Inval	char
	`Junk	char
	`Trunc	bio.err
	`Unexpected (char, char)
	`NYI
;;

type state = union
	`Open
	`Close
;;

type event = union
	`Start		(byte[:], (byte[:], byte[:])[:])
	`Characters	byte[:]
;;

const parse = {x
	match x.state
	| `Open:	-> open(x)
	| `Close:	-> close(x)
	;;
}

const close = {x
	x.state = `Open

	match bio.readto(x.file, "<")
	| `std.Ok s:
		if std.strstrip(s).len > 0
			-> `std.Ok `Characters s
		else
			std.slfree(s)
			-> parse(x)
		;;
	| `std.Err e:
		-> `std.Err error(x, `Trunc e)
	;;
}

const open = {x
	x.state = `Close

	match bio.peekc(x.file)
	| `std.Ok '!':	/* XXX */
	| `std.Ok '/':	/* XXX */
	| `std.Ok '?':	/* XXX */
	| `std.Ok '>':	-> `std.Err error(x, `Empty)
	| `std.Ok _:	-> elemname(x)
	| `std.Err e:	-> `std.Err error(x, `Trunc e)
	;;

	-> `std.Err error(x, `NYI)
}

const elemname = {x
	var start, stop
	var attrs = [][:]

	match bio.readto(x.file, ">")
	| `std.Ok s:	newbuf(x, s)
	| `std.Err e:	-> `std.Err error(x, `Trunc e)
	;;

	match bnext(x)
	| c:	if namestartchar(c)
			start =  x.bcur
			stop = x.bidx
		else
			-> `std.Err error(x, `Inval c)
		;;
	;;
	while x.bidx < x.buf.len
		var c = bnext(x)
		if namechar(c)
			stop = x.bidx
		elif isspace(c)
			match attributes(x, &attrs)
			| `std.Ok _:
			| `std.Err e:
				std.slfree(attrs)
				-> `std.Err e
			;;
			break
		else
			-> `std.Err error(x, `Inval c)
		;;
	;;

	-> `std.Ok `Start (x.buf[start:stop], attrs)
}

const attributes = {x, attrs
	var name, val

	eatspace(x)

	while x.bidx < x.buf.len
		match bpeek(x)
		| '/':	-> `std.Err error(x, `NYI)	/* XXX */
		| _:
		;;

		match attrname(x)
		| `std.Ok n:	name = n
		| `std.Err e:	-> `std.Err e
		;;

		match attrval(x)
		| `std.Ok v:	val = v
		| `std.Err e:	-> `std.Err e
		;;

		std.slpush(attrs, (name, val))

		eatspace(x)
	;;

	-> `std.Ok void
}

const attrname = {x
	var start, stop
	var spaced = false

	eatspace(x)

	match bnext(x)
	| '/':	-> `std.Err error(x, `NYI)	/* XXX */
	| c:
		if namestartchar(c)
			start =  x.bcur
			stop = x.bidx
		else
			-> `std.Err error(x, `Inval c)
		;;
	;;

	while x.bidx < x.buf.len
		match bnext(x)
		| '=':	-> `std.Ok x.buf[start:stop]
		| c:
			if spaced
				-> `std.Err error(x, `Unexpected (c, '='))
			elif namechar(c)
				stop = x.bidx
			elif isspace(c)
				spaced = true
				eatspace(x)
			else
				-> `std.Err error(x, `Inval c)
			;;
		;;
	;;

	-> `std.Err error(x, `Trunc `bio.Eof)
}

const attrval = {x
	var start, stop
	var q

	eatspace(x)

	match bnext(x)
	| '"':	q = '"'
	| '\'':	q = '\''
	| c:	-> `std.Err error(x, `Unexpected (c, '"'))
	;;

	start = stop = x.bidx

	while x.bidx < x.buf.len
		if bnext(x) == q
			-> `std.Ok x.buf[start:stop]
		else
			stop = x.bidx
		;;
	;;

	-> `std.Err error(x, `Trunc `bio.Eof)
}

/*const fnext = {x
	var c

	match bio.getc(x.file)
	| `std.Ok a:	c = a
	| `std.Err e:	-> `std.Err error(x, e)
	;;

	if c == '\n'
		x.line++
		x.off = 0
	else
		x.off++
	;;

	-> `std.Ok c
}*/

const bpeek = {x
	if x.bidx < x.buf.len
		-> std.decode(x.buf[x.bidx:])
	else
		-> std.Badchar
	;;
}

const bnext = {x
	if x.bidx >= x.buf.len
		-> std.Badchar
	;;

	var c = std.decode(x.buf[x.bidx:])
	x.bcur = x.bidx
	x.bidx += std.charlen(c)

	if c == '\n'
		x.line++
		x.off = 0
	else
		x.off++
	;;

	-> c
}

const newbuf = {x, buf
	std.slfree(x.buf)
	x.bidx = x.bcur = 0
	x.buf = buf
}

const eatspace = {x
	while true
		match bpeek(x)
		| ' ':
		| '\n':
		| '\t':
		| '\r':
		| _:	break
		;;

		bnext(x)
	;;
}

/* like bio.skipspace but includes \r and increments the offsets */
const skipspace = {x
	while true
		match bio.peekc(x.file)
		| `std.Ok ' ':
		| `std.Ok '\n':
			x.line++
			x.off = 0
			goto nl
		| `std.Ok '\t':
		| `std.Ok '\r':
		| `std.Ok _:
			break
		| `std.Err _:
			break
		;;

		x.off++
:nl
		bio.getc(x.file)
	;;
}

const error = {x, e
	-> [.line = x.line, .off = x.off, .err = e]
}

const free = {x
	bio.close(x.file)
	std.slfree(x.buf)
	std.free(x)
}

const mkbuf = {s
	var x : xml#

	x = std.mk([
		.file = bio.mkmem(s),
		.buf = [][:],
		.bidx = 0,
		.bcur = 0,
		.line = 1,
		.off = 0,
		.state = `Close
	])

	skipspace(x)

	-> x
}
