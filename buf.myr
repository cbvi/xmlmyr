use std
use bio

use "err"
use "chars"
use "types"

pkg xml =
	pkglocal const newbuf :		(x : ctx#, buf : byte[:] -> void)
	pkglocal const expandbuf :	(x : ctx#, buf : byte[:] -> void)
	pkglocal const fillbuf :	(x : ctx#, full : byte[:], pref : byte[:], suf : byte[:] -> std.result(byte[:], err))

	pkglocal const bexpect :	(x : ctx#, want : char -> std.result(void, errtype))
	pkglocal const bnext :		(x : ctx# -> char)
	pkglocal const bpeek :		(x : ctx# -> char)
	pkglocal const beatspace :	(x : ctx# -> void)
	pkglocal const bskip :		(x : ctx#, n : int -> void)

	pkglocal const startname:	(x : ctx# -> std.result((std.size, std.size), err))
;;

const newbuf = {x, buf
	std.slfree(x.p.buf)
	x.p.bidx = 0
	x.p.buf = buf
}

const expandbuf = {x, buf
	x.p.buf = std.sljoin(&x.p.buf, buf)
	std.slfree(buf)
}

const fillbuf = {x, full, pref, suf
	var start, stop

	for p : std.bychar(pref)
		match bio.getc(x.file)
		| `std.Ok c:
			x.p.off += 1
			if c != p
				-> `std.Err error(x, `Unexpected (c, p))
			;;
		| `std.Err e:
			-> `std.Err error(x, `Trunc e)
		;;
	;;


	match bio.readterm(x.file, suf)
	| `std.Ok s:	newbuf(x, s)
	| `std.Err e:	-> `std.Err error(x, `Trunc e)
	;;

	start = x.p.bidx

	while x.p.bidx < x.p.buf.len - suf.len
		bnext(x)
	;;

	stop = x.p.bidx

	for s : std.bychar(suf)
		if s != bnext(x)
			-> `std.Err error(x, `Unclosed (full, suf))
		;;
	;;

	-> `std.Ok x.p.buf[start:stop]
}

const bexpect = {x, want
	var c

	if x.p.bidx < x.p.buf.len
		c = bnext(x)
		if c == want
			-> `std.Ok void
		else
			-> `std.Err `Unexpected (c, want)
		;;
	else
		-> `std.Err `Trunc `bio.Eof
	;;
}

const bnext = {x
	var c

	if x.p.bidx >= x.p.buf.len
		-> std.Badchar
	;;

	c = std.decode(x.p.buf[x.p.bidx:])
	x.p.bidx += std.charlen(c)

	match c
	| '\n':
		x.p.line++
		x.p.off = 0
	| _:
		x.p.off++
	;;

	-> c
}

const bpeek = {x
	if x.p.bidx < x.p.buf.len
		-> std.decode(x.p.buf[x.p.bidx:])
	else
		-> std.Badchar
	;;
}

const beatspace = {x
	var p
	while true
		p = bpeek(x)
		match p
		| ' ':
		| '\n':
			x.p.line++
			x.p.off = 0
			goto next
		| '\t':
		| '\r':
		| _:
			break
		;;
		x.p.off++
:next
		x.p.bidx += std.charlen(p)
	;;
}

const bskip = {x, n
	for var i = 0; i < n; i++
		bnext(x)
	;;
}

const startname = {x
	var c
	var start

	if x.p.bidx >= x.p.buf.len
		-> `std.Err error(x, `Trunc `bio.Eof)
	;;

	start = x.p.bidx
	c = bnext(x)
	if namestartchar(c)
		-> `std.Ok (start, x.p.bidx)
	else
		-> `std.Err error(x, `Inval c)
	;;
}
